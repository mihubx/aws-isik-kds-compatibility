/// url = "http://mihubx.de/fhir/StructureMap/MHX_SM_IsikToKds"
/// name = "MHX_SM_IsikToKds"
/// title = "ISiK Basismodul 3.0.0 to MII KDS Person 1.0.16 Conversions"
/// status = "draft"

uses "https://gematik.de/fhir/isik/v3/Basismodul/StructureDefinition/ISiKPatient" alias ISiKPatient as source
uses "https://www.medizininformatik-initiative.de/fhir/core/modul-person/StructureDefinition/Patient" alias KDSPatient as target

group MapPatient(source src: ISiKPatient, target tgt: KDSPatient) {
    src.id as s_id -> tgt.id = s_id;
    src.meta as s_meta -> tgt.meta as t_meta then MapMeta(s_meta, t_meta);
    src.text as s_text -> tgt.text = s_text;
    src -> tgt then CreateAbsentOrganization(src, tgt) "fix-absent-managing-org";
    src -> tgt then MapPatientIdentifier(src, tgt) "fix-patient-identifier";
    src.active -> tgt.active;
    src.name as s_name -> tgt.name = s_name;
    src.telecom as s_telecom -> tgt.telecom = s_telecom;
    src.gender as s_gender -> tgt.gender = s_gender;
    src.birthDate as s_date -> tgt.birthDate = s_date;
    src.deceasedDateTime as s_deceased -> tgt.deceasedDateTime = s_deceased;
    src -> tgt then MapPatientAddress(src, tgt) "fix-patient-address";
    src.martialStatus as s_martialStatus -> tgt.martialStatus = s_martialStatus;
    src.photo as s_photo -> tgt.photo as s_photo;
    src.communication as s_communication -> tgt.communication = s_communication;
    src.generalPractitioner as s_generalPractitioner -> tgt.generalPractitioner = s_generalPractitioner;
}

group CreateAbsentOrganization(source src: AWSPatient, target tgt: ISiKPatient) {
    src -> tgt.contained = create("Organization") as t_organization,
        t_organization.id = "absent-org",
        t_organization.name = "Fehlende/ unbekannte Organisation",
        t_organization.extension = create("Extension") as t_ext,
        t_ext.url = "http://hl7.org/fhir/StructureDefinition/data-absent-reason",
        t_ext.value = create("code") as t_value,
        t_value.value = "not-applicable" "create-absent-org";
}

group MapMeta(source src: Meta, target tgt: Meta) {
    src.lastUpdated as s_lastUpdated -> tgt.lastUpdated = s_lastUpdated;
    src.versionId as s_versionId -> tgt.versionId = s_versionId;
    src.source as s_source -> tgt.source = s_source;
    src.profile as s_profile -> tgt.profile = "https://www.medizininformatik-initiative.de/fhir/core/modul-person/StructureDefinition/Patient";
    src.security as s_security -> tgt.security = s_security;
    src.tag as s_tag -> tgt.tag = s_tag;
}

group MapPatientIdentifier(source src: AWSPatient, target tgt: ISiKPatient) {
    // Fix: Perform KVID patient identifier mapping with expected system
    src -> tgt then FixGkvPatientIdentifier(src, tgt) "recommendation-3";
    // Otherwise copy patient identifier
    src.identifier as s_identifier where (($this.system = "http://fhir.de/sid/gkv/kvid-10") = false) -> tgt.identifier = s_identifier;
}

group FixGkvPatientIdentifier(source src: ISiKPatient, target tgt: KDSPatient) {
    src.identifier as s_kvid where ($this.system = "http://fhir.de/sid/gkv/kvid-10")
        -> tgt.identifier as t_kvid then {
            s_kvid.use as s_use -> t_kvid.use = s_use;
            s_kvid.type as s_type -> t_kvid.type = s_type;
            s_kvid.value as s_value -> t_kvid.value = s_value;
            // Fix 1: Substitute naming system with expected naming system
            s_kvid.system -> t_kvid.system = "http://fhir.de/NamingSystem/gkv/kvid-10";
            // Fix 2: Copy assigner if exists, otherwise create dummy managing organization with invalid IKNR
            s_kvid.assigner as s_assigner -> t_kvid.assigner = s_assigner;
            s_kvid where $this.assigner.exists().not()
                -> t_kvid.assigner = create("Reference") as t_reference,
                   t_reference.reference = "#absent-org",
                   t_reference.display = "Fehlende/ unbekannte Organisation",
                   t_reference.identifier = create("Identifier") as t_iknr,
                   t_iknr.system = "http://fhir.de/NamingSystem/arge-ik/iknr",
                   t_iknr.value = "000000000",
                   t_iknr.extension = create("Extension") as t_ext,
                   t_ext.url = "http://hl7.org/fhir/StructureDefinition/data-absent-reason",
                   t_ext.value = create("code") as t_value,
                   t_value.value = "not-applicable" "recommendation-4";
        } "patient-kvid";
}

group MapPatientAddress(source src: AWSPatient, target tgt: ISiKPatient) {
    // Copy any address element, if exists
    src.address as s_address -> tgt.address = s_address;
    // Otherwise create an empty address element with data absent reason
    src where $this.address.exists().not() -> tgt then CreateAbsentAddress(src, tgt) "recommendation-5";
}

group CreateAbsentAddress(source src: AWSPatient, target tgt: ISiKPatient) {
    src -> tgt.address = create("Address") as t_address,
        t_address.text = "Keine Adresse vorhanden" then CreateDataAbsentNotApplicable(src, t_address) "data-absent-not-applicable";
}

group CreateDataAbsentNotApplicable(source src: AWSPatient, target tgt: Address) {
    src -> tgt.extension = create("Extension") as t_ext,
        t_ext.url = "http://hl7.org/fhir/StructureDefinition/data-absent-reason",
        t_ext.value = create("code") as t_value,
        t_value.value = "not-applicable" "create-data-absent-extension";
}